component io_muxn "Gate one of N input values";
pin in s32 sel = 0;
pin io float out "Follows the value of \\fBin<M>\\fR whereas M is the value of the \\fBsel\\fR input. \
If \\fBsel\\fR is not in the range of available inputs 0 is output.";
pin io float in#.[pincount];

variable hal_s32_t last_sel = 0;
variable hal_float_t last_in = 0.0;
variable hal_float_t last_out = 0.0;

instanceparam int pincount = 8;

option MAXCOUNT 16;
option DEFAULTCOUNT 2;

function _;
license "GPL";
author "Alexander Roessler";
;;
FUNCTION(_)
{
    if ((sel < 0) || (sel >= localpincount))
    {
        return 0; // incorrect sel, we change nothing
    }

    if (sel != last_sel)
    {
        out = in_(sel);
        last_sel = sel;
        last_out = out;
        last_in = in_(sel);
    }
    else
    {
        if (last_out != out)
        {
            in_(sel) = out;
            last_out = out;
            last_in = out;
        }
        else if (last_in != in_(sel))
        {
            out = in_(sel);
            last_in = in_(sel);
            last_out = in_(sel);
        }
    }

    return 0;
}
